import com.vanniktech.maven.publish.SonatypeHost
import com.vanniktech.maven.publish.AndroidSingleVariantLibrary

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'com.vanniktech.maven.publish'
    id 'signing'
}

android {
    namespace 'io.github.andreweveld.easypreferences'
    compileSdk 34

    defaultConfig {
        minSdk 24
        targetSdk 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

mavenPublishing {
    configure(new AndroidSingleVariantLibrary("release", true, true))
    coordinates("io.github.andreweveld", "easypreferences", "1.0.0")
    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)

    pom {
        name = "Easy Preferences"
        description = "Simple preferences implementation."
        inceptionYear = "2024"
        url = "https://github.com/AndrewEveld/easy-preferences"
        licenses {
            license {
                name = "MIT License"
                url = "https://github.com/AndrewEveld/easy-preferences/blob/main/LICENSE"
                distribution = "https://github.com/AndrewEveld/easy-preferences/blob/main/LICENSE"
            }
        }
        developers {
            developer {
                id = "AndrewEveld"
                name = "Andrew Eveld"
                url = "https://github.com/AndrewEveld/"
            }
        }
        scm {
            url = "https://github.com/AndrewEveld/easy-preferences"
            connection = "scm:git:git://github.com/AndrewEveld/easy-preferences"
            developerConnection = "scm:git:ssh://git@github.com/AndrewEveld/easy-preferences"
        }
    }
    signAllPublications()
}

dependencies {
    var mockkVersion = "1.13.10"
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    testImplementation "io.mockk:mockk-android:${mockkVersion}"
    testImplementation "io.mockk:mockk-agent:${mockkVersion}"
}